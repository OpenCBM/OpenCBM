        *=$0700

FAST_IEC_AS_INPUT	:= $81b2
FAST_IEC_AS_OUTPUT	:= $81ce

CIA		:= $4000
CIA_PB		:= CIA + $0
CIA_PA		:= CIA + $1
CIA_DDRB	:= CIA + $2
CIA_DDRA	:= CIA + $3
CIA_TALO	:= CIA + $4
CIA_TAHI	:= CIA + $5
CIA_TBLO	:= CIA + $6
CIA_TBHI	:= CIA + $7
CIA_TOD10THS	:= CIA + $8
CIA_TODSEC	:= CIA + $9
CIA_TODMIN	:= CIA + $A
CIA_TODHR	:= CIA + $B
CIA_SDR		:= CIA + $C
CIA_ICR		:= CIA + $D
CIA_CRA		:= CIA + $E
CIA_CRB		:= CIA + $F

CIA_ICR_B_SP	:= $08
CIA_CRA_B_SP_OUTPUT	:= $40

IEC_PORT	:= $1800

IEC_DATA_IN	:= $01
IEC_DATA_OUT	:= $02
IEC_CLOCK_IN	:= $04
IEC_CLOCK_OUT	:= $08
IEC_ATNA	:= $10
IEC_ATN_IN	:= $80

        jmp gts         ; get track/sector
        jmp gbyte       ;
        jmp gblk        ; receive block
        jmp sbyte       ; send byte
        jmp sblk        ; send block

; init:
        lda #IEC_CLOCK_IN
        jsr wait_for_line_inactive
        jsr FAST_IEC_AS_OUTPUT
        jsr FAST_IEC_AS_INPUT
        lda #<$0001
        sta CIA_TALO
        lda #>$0001
        sta CIA_TAHI
        lda #CIA_ICR_B_SP
        sta CIA_ICR
        jmp set_data_out

gts:
        jsr gbyte
        pha
        jsr gbyte
        tay
        pla
        tax
        rts

gblk:
        jsr gbyte
        sta ($30),y
        iny
        bne gblk
        rts

gbyte:
        lda #IEC_CLOCK_IN
        jsr wait_for_line_active
        bit CIA_ICR
        jsr unset_data_clock
        lda #CIA_ICR_B_SP
@wait_for_sp_end:
        bit CIA_ICR
        beq @wait_for_sp_end
        lda CIA_SDR
        pha
        lda #IEC_CLOCK_IN
        jsr wait_for_line_inactive
        jsr set_data_out
        pla
        rts

sbyte:
        pha
        lda #IEC_CLOCK_IN
        jsr wait_for_line_active
        jsr set_clock_out
        lda #IEC_DATA_IN
        jsr wait_for_line_active
        jsr FAST_IEC_AS_OUTPUT
        jsr unset_data_clock
        lda #IEC_CLOCK_IN
        jsr wait_for_line_active
        pla
        sta CIA_SDR
        lda #IEC_CLOCK_IN
        jsr wait_for_line_inactive
        jsr set_clock_out
        jsr FAST_IEC_AS_INPUT
        jmp set_data_out

sblk:
        lda #IEC_CLOCK_IN
        jsr wait_for_line_active
        jsr set_clock_out
        lda #IEC_DATA_IN
        jsr wait_for_line_active
        jsr FAST_IEC_AS_OUTPUT
        jmp @br2

@br1:
        lda CIA_CRA
        and #(~ CIA_CRA_B_SP_OUTPUT) & $FF
        sta CIA_CRA
        jsr set_data_out
        lda #IEC_CLOCK_IN
        jsr wait_for_line_active
        jsr set_clock_out
        lda #IEC_DATA_IN
        jsr wait_for_line_active
        lda CIA_CRA
        ora #CIA_CRA_B_SP_OUTPUT
        sta CIA_CRA
@br2:
        jsr unset_data_clock
        lda #IEC_CLOCK_IN
        jsr wait_for_line_active
        lda ($30),y
        sta CIA_SDR
        lda #IEC_CLOCK_IN
        jsr wait_for_line_inactive
        jsr set_clock_out
        iny
        bne @br1
        jsr FAST_IEC_AS_INPUT
        jmp set_data_out

wait_for_line_inactive:
        bit IEC_PORT
        bne wait_for_line_inactive
        rts

wait_for_line_active:
        bit IEC_PORT
        beq wait_for_line_active
        rts

set_data_out:
        lda #IEC_DATA_OUT
        .byte $2c

unset_data_clock:
        lda #$00
        sta IEC_PORT
        rts

set_clock_out:
        lda #IEC_CLOCK_OUT
        sta IEC_PORT
        rts
